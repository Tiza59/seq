#!/opt/maths/bin/perl -w
use strict;

=head1 square loop

Find n such that there exists a loop of the integers 1 .. n in which each
pair of consecutive values sums to a square.

Bonus: find also n where a broken loop exists.

This code finds first broken loop at n=17, with the sequence:
  16 9 7 2 14 11 5 4 12 13 3 6 10 15 1 8 17

Since the first squares > 18 are 25, 36, 49, this implies the first loop
must have n >= 31.

=cut

my %sq;
my @sq;
my @pair;
my $lastsq = 0;

my $n;
for ($n = 1; 1; ++$n) {
    while ($lastsq * $lastsq < $n + $n) {
        ++$lastsq;
        push @sq, $lastsq * $lastsq;
        $sq{$lastsq * $lastsq} = 1;
    }

    shift @sq while @sq && $sq[0] <= $n;
    for (@sq) {
        my $other = $_ - $n;
        next if $other == $n;
        last if $other > $n;
        push @{ $pair[$other] }, $n;
        push @{ $pair[$n] }, $other;
    }

    my %req = map +($_ => 1), 1 .. $n;
    my $result;
    for my $this (1 .. $n) {
        $result = try($this, \%req, $n - 1);
        last if $result;
    }
    print "$n: $result\n";
}

sub try {
    my($this, $need, $remain) = @_;
    if ($remain) {
        local $need->{$this} = 0;
        for my $next (@{ $pair[$this] }) {
            next unless $need->{$next};
            my $result = try($next, $need, $remain - 1);
            return $result if $result;
        }
        return 0;
    }
    return 1;
}
