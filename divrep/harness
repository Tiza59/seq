#!/opt/maths/bin/perl
use strict;
use warnings;
use lib ('../lib', 'lib');
use Seq::Db;
use H::Heap;
use Math::GMP;
use POSIX qw{ :sys_wait_h };

my($opt_recreate, $debug, $opt_strategy) = (0, 0, 0);
my($maxproc, $maxrun, $drain) = (10, 100, 0);
$SIG{'HUP'} = sub { $drain = 1 };
while (@ARGV && $ARGV[0] =~ /^-/) {
    my $arg = shift(@ARGV);
    last if $arg eq '--';
    $opt_recreate = 1, next if $arg eq '-r';
    $opt_strategy = 1, next if $arg eq '-s';
    $debug = 1, next if $arg eq '-d';
    die "Unknown option '$arg'";
}
$maxproc = shift(@ARGV) if @ARGV;
$maxrun = shift(@ARGV) if @ARGV;

local @ENV{qw{ DBI_TRACE DBIC_TRACE }} = (1, 1) if $debug;
my $db = Seq::Db->new({
    db => 'tauseq',
    user => 'hv',
    passwd => 'hv',
    recreate => $opt_recreate,
});

use Data::Dumper;
local $Data::Dumper::Indent = $Data::Dumper::Sortkeys = 1;

Seq::Run->restrategise($db) if $opt_strategy;
my $prio = H::Heap->new(q{ $b->priority <=> $a->priority });
$prio->insert(Seq::Starter->new(2, $maxproc));
my(%pid, @runnable);

while (1) {
    if ($drain) {
        printf "Draining %s processes\n", 0 + keys %pid;
        waitFor($db, \%pid, 1) while keys %pid;
        last;
    }

    if (@runnable) {
        if (keys(%pid) >= $maxproc) {
            waitFor($db, \%pid, 1);
        } else {
            my $r = shift @runnable;
            my $pid = $r->run;
            if ($pid) {
                $pid{$pid} = $r;
                $drain = 1, next if --$maxrun <= 0;
            }
        }
    } else {
        my $obj = $prio->fetch;
        $prio->insert($_) for $obj->prep($db);
        push @runnable, $obj->runnable;
    }
}
exit 0;

sub waitFor {
    my($db, $pids, $block) = @_;
    my @t0 = times;
    my $pid = waitpid(-1, $block ? 0 : WNOHANG);
    return 0 if $pid < 0;    

    my $obj = delete $pids->{$pid};
    my @t1 = times;
    my $time = $t1[2] + $t1[3] - $t0[2] - $t0[3];
    $prio->insert($_) for $obj->finalize($db, $time);
    return 1;
}

package Seq::Starter {
    sub new {
        my($class, $start, $count) = @_;
        return bless {
            start => $start,
            count => $count,
        }, $class;
    }
    sub priority {
        my($self) = @_;
        return $self->{priority} //= Seq::TauG->priority($self->{start});
    }
    sub prep {
        my($self, $db) = @_;
        my $start = $self->{start};
        my $count = $self->{count};
        my $end = $start + $count - 1;
        my $max = Seq::TauG->max_known($db);
        if ($end > $max) {
            Seq::TauG->genTo($db, $end);
        }
        return (
            Seq::Starter->new($end + 1, 1),
            Seq::TauG->range($db, $start, $end),
        );
    }
    sub runnable { () }
};

